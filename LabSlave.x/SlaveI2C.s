;LAB 11 - Slave (Interrupt Version)
;Jacob Horsley
;RCET 3375
;Fifth Semester
;I2C Communication (Slave - Interrupt Mode)
;Git URL: https://github.com/horsjaco117/Assembly_Code
    
;Device Setup
;--------------------------------------------------------------------------
;Configuration (unchanged)
    CONFIG FOSC = HS
    CONFIG WDTE = OFF
    CONFIG PWRTE = OFF
    CONFIG MCLRE = ON 
    CONFIG CP = OFF
    CONFIG CPD = OFF
    CONFIG BOREN = OFF
    CONFIG IESO = OFF
    CONFIG FCMEN = OFF
    CONFIG LVP = OFF
; CONFIG2
    CONFIG BOR4V = BOR40V
    CONFIG WRT = OFF
;Include Statements
#include <xc.inc>
;Code Section
;--------------------------------------------------------------------------
 
;Register/Variable Setup
  SOMEVALUE EQU 0x5f
  RECEIVED_DATA EQU 0x27
  W_TEMP EQU 0x21
  STATUS_TEMP EQU 0x22
  PULSE_SELECT EQU 0x26 ; Not used, but retained for compatibility
  COUNT1 EQU 0x20
  TEMP EQU 0x28
  SERVO_ID EQU 0x29
  POS EQU 0x2A
  SERVO_POS1 EQU 0x2B
  SERVO_POS2 EQU 0x2C
  SERVO_POS3 EQU 0x2D
  SERVO_COUNT1 EQU 0x2E
  SERVO_COUNT2 EQU 0x2F
  SERVO_COUNT3 EQU 0x30
  STATE EQU 0x31
  PHASE_COUNT_LOW EQU 0x32
  PHASE_COUNT_HIGH EQU 0x33
  SPACE_LOW EQU 0x34
  SPACE_HIGH EQU 0x35
  TEMP_LOW EQU 0x36
  TEMP_HIGH EQU 0x37
  NEW_DATA EQU 0x38
  FLAGS EQU 0x39

;---------------------------------------------------------------------
; Reset & Interrupt vectors
;---------------------------------------------------------------------
PSECT resetVect, class=CODE, delta=2
    GOTO Start
PSECT isrVect, class=CODE, delta=2
    GOTO INTERRUPT
Start:
Setup:
  BSF STATUS, 5
  BSF STATUS, 6
  MOVLW 0x00
  MOVWF TRISB
  CLRF ANSELH
  CLRF OPTION_REG
  BCF STATUS, 5
  CLRF CM2CON1
  BSF STATUS, 5
  BCF STATUS, 6
  MOVLW 0x00
  MOVWF TRISA
  CLRF ANSEL
  MOVLW 0xFF
  MOVWF WPUB
  CLRF IOCB
  CLRF PSTRCON
  MOVLW 0x18
  MOVWF TRISC
  MOVLW 0x0B ; SSPIE=1, TMR2IE=1
  MOVWF PIE1
  MOVLW 0x08
  MOVWF PIE2
  CLRF SSPCON2
  MOVLW 0x80
  MOVWF SSPSTAT
  MOVLW 0x40
  MOVWF SSPADD
  MOVLW 0x3F ; PR2 for small interval (64 ticks)
  MOVWF PR2
  BCF STATUS, 5
  BCF STATUS, 6
  MOVLW 0x00
  MOVWF TMR2
  MOVLW 0x04 ; TMR2ON=1, T2CKPS=00 (prescale 1)
  MOVWF T2CON
  CLRF CCP1CON
  CLRF PORTC
  CLRF CCP2CON
  CLRF PORTB
  CLRF RCSTA
  CLRF T1CON
  CLRF PIR1
  CLRF PIR2
  CLRF PORTA
  CLRF RECEIVED_DATA
  MOVLW 0x26
  MOVWF SSPCON
  MOVLW 0xC0 ; GIE=1, PEIE=1
  MOVWF INTCON
  ; Initialize servo positions to 0
  CLRF SERVO_POS1
  CLRF SERVO_POS2
  CLRF SERVO_POS3
  ; Set initial counts (for pos=0)
  MOVLW 39
  MOVWF SERVO_COUNT1
  MOVWF SERVO_COUNT2
  MOVWF SERVO_COUNT3
  ; Calculate initial space
  CALL CALC_SPACE
  ; Start with state 3 (space), phase count to space
  MOVLW 0x03
  MOVWF STATE
  MOVF SPACE_LOW, W
  MOVWF PHASE_COUNT_LOW
  MOVF SPACE_HIGH, W
  MOVWF PHASE_COUNT_HIGH
  CLRF NEW_DATA
  CLRF FLAGS

MAINLOOP:
  BTFSS FLAGS, 0
  GOTO MAINLOOP
  BCF FLAGS, 0
  BCF INTCON, 7
  MOVF NEW_DATA, W
  MOVWF PORTB
  ANDLW 0x03
  MOVWF SERVO_ID
  SWAPF NEW_DATA, W
  ANDLW 0x0F
  MOVWF POS
  MOVF SERVO_ID, W
  SUBLW 0x02
  BTFSS STATUS, 0
  GOTO DONE_UPDATE
  MOVF SERVO_ID, W
  ADDWF PCL, F
  
  
  ;GOTO UPDATE_SERVO1
  GOTO DONE_UPDATE
  GOTO UPDATE_SERVO2
  GOTO UPDATE_SERVO1
  GOTO UPDATE_SERVO3
 ; GOTO DONE_UPDATE
UPDATE_SERVO1:
  MOVF POS, W
  SUBWF SERVO_POS1, W
  BTFSC STATUS, 2
  GOTO DONE_UPDATE
  MOVF POS, W
  MOVWF SERVO_POS1
  CALL LOOKUP_COUNT
  MOVWF SERVO_COUNT1
  CALL CALC_SPACE
  GOTO DONE_UPDATE
UPDATE_SERVO2:
  MOVF POS, W
  SUBWF SERVO_POS2, W
  BTFSC STATUS, 2
  GOTO DONE_UPDATE
  MOVF POS, W
  MOVWF SERVO_POS2
  CALL LOOKUP_COUNT
  MOVWF SERVO_COUNT2
  CALL CALC_SPACE
  GOTO DONE_UPDATE
UPDATE_SERVO3:
  MOVF POS, W
  SUBWF SERVO_POS3, W
  BTFSC STATUS, 2
  GOTO DONE_UPDATE
  MOVF POS, W
  MOVWF SERVO_POS3
  CALL LOOKUP_COUNT
  MOVWF SERVO_COUNT3
  CALL CALC_SPACE
  GOTO DONE_UPDATE
DONE_UPDATE:
  BSF INTCON, 7
  GOTO MAINLOOP

INTERRUPT:
  MOVWF W_TEMP
  SWAPF STATUS, W
  MOVWF STATUS_TEMP
  BTFSC PIR1, 1
  GOTO HANDLE_SERVO
  BTFSC PIR1, 3
  GOTO HANDLE_I2C
  BTFSC PIR2, 3
  GOTO HANDLE_BCL
  GOTO INTERRUPT_END

HANDLE_SERVO:
  BCF PIR1, 1
  CLRF TMR2
  ; Proper 16-bit decrement
  MOVLW 0x01
  SUBWF PHASE_COUNT_LOW, F
  BTFSC STATUS, 0
  GOTO NO_BORROW
  DECF PHASE_COUNT_HIGH, F
NO_BORROW:
  ; Check if both bytes are zero
  MOVF PHASE_COUNT_LOW, F
  BTFSS STATUS, 2
  GOTO INTERRUPT_END
  MOVF PHASE_COUNT_HIGH, F
  BTFSS STATUS, 2
  GOTO INTERRUPT_END
  ; Phase end - switch state
  MOVF STATE, W
  ADDWF PCL, F
  GOTO STATE0_END
  GOTO STATE1_END
  GOTO STATE2_END
  GOTO STATE3_END

STATE0_END:
  BCF PORTA, 0
  BSF PORTA, 1
  MOVF SERVO_COUNT2, W
  MOVWF PHASE_COUNT_LOW
  CLRF PHASE_COUNT_HIGH
  MOVLW 0x01
  MOVWF STATE
  GOTO INTERRUPT_END

STATE1_END:
  BCF PORTA, 1
  BSF PORTA, 2
  MOVF SERVO_COUNT3, W
  MOVWF PHASE_COUNT_LOW
  CLRF PHASE_COUNT_HIGH
  MOVLW 0x02
  MOVWF STATE
  GOTO INTERRUPT_END

STATE2_END:
  BCF PORTA, 2
  MOVF SPACE_LOW, W
  MOVWF PHASE_COUNT_LOW
  MOVF SPACE_HIGH, W
  MOVWF PHASE_COUNT_HIGH
  MOVLW 0x03
  MOVWF STATE
  GOTO INTERRUPT_END

STATE3_END:
  BSF PORTA, 0
  MOVF SERVO_COUNT1, W
  MOVWF PHASE_COUNT_LOW
  CLRF PHASE_COUNT_HIGH
  CLRF STATE
  GOTO INTERRUPT_END

HANDLE_I2C:
  BCF PIR1, 3
  BSF STATUS, 5
  BCF STATUS, 6
  BTFSC SSPSTAT, 4
  CLRF SSPSTAT
  BTFSC SSPSTAT, 7
  CLRF SSPSTAT
  BCF STATUS, 5
  BCF STATUS, 6
  BTFSC SSPSTAT, 5
  GOTO READ_DATA
  MOVF SSPBUF, W
  GOTO INTERRUPT_END
READ_DATA:
  MOVF SSPBUF, W
  MOVWF NEW_DATA
  SUBLW 0x24
  BTFSS STATUS, 2
  BSF FLAGS, 0
  GOTO INTERRUPT_END

HANDLE_BCL:
  BCF PIR2, 3
  BCF SSPCON, 5
  BSF SSPCON, 5
  GOTO INTERRUPT_END

INTERRUPT_END:
  SWAPF STATUS_TEMP, W
  MOVWF STATUS
  SWAPF W_TEMP, F
  SWAPF W_TEMP, W
  RETFIE

CALC_SPACE:
  MOVLW 0x1B ; Low byte of 1563 (0x061B)
  MOVWF TEMP_LOW
  MOVLW 0x06 ; High byte
  MOVWF TEMP_HIGH
  MOVF SERVO_COUNT1, W
  SUBWF TEMP_LOW, F
  BTFSC STATUS, 0
  GOTO NO_BORROW1
  DECF TEMP_HIGH, F
NO_BORROW1:
  MOVF SERVO_COUNT2, W
  SUBWF TEMP_LOW, F
  BTFSC STATUS, 0
  GOTO NO_BORROW2
  DECF TEMP_HIGH, F
NO_BORROW2:
  MOVF SERVO_COUNT3, W
  SUBWF TEMP_LOW, F
  BTFSC STATUS, 0
  GOTO NO_BORROW3
  DECF TEMP_HIGH, F
NO_BORROW3:
  MOVF TEMP_LOW, W
  MOVWF SPACE_LOW
  MOVF TEMP_HIGH, W
  MOVWF SPACE_HIGH
  RETURN

LOOKUP_COUNT:
  ADDWF PCL, F
  RETLW 39  ; pos 0
  RETLW 49  ; 1
  RETLW 60  ; 2
  RETLW 70  ; 3
  RETLW 80  ; 4
  RETLW 91  ; 5
  RETLW 101 ; 6
  RETLW 111 ; 7
  RETLW 122 ; 8
  RETLW 132 ; 9
  RETLW 142 ; 10
  RETLW 153 ; 11
  RETLW 163 ; 12
  RETLW 173 ; 13
  RETLW 184 ; 14
  RETLW 194 ; 15

END
